PARAMINT	type	0
PARAMINT	delayTime	30
PARAMFLOAT liftheightmetres 10.0
PARAMFLOAT liftspeed 0.05
LOCALFLOAT startz
LOCALFLOAT liftheightframes
LOCALFLOAT liftframecount
LOCALFLOAT tempf
LOCALFLOAT heightAdd
LOCALINT	tempi
LOCALINT	pn

DEFINE	ACC	0.01

STATE Init
	MyFlag2 = MyFlag2 | HOLDPLAYER
	IF (!(MyFlag & OWNFPARAMS))
		liftheightmetres = 10.0
		liftspeed = 0.05
	ENDIF

	IF (!(MyFlag2 & OWNIPARAMS))
		type = 0
		delayTime = 30
	ENDIF
	IAmHoldPlayer()
	startz = z
	liftheightframes = ( liftheightmetres / liftspeed )
	MyFlag = MyFlag | ENEMY | STRATCOLL
	RegisterCollision()
   	MyFlag2 = MyFlag2 | COLLMOVE | TRANSLUCENT
	TRIGSET>CalcCollPoly EVERY 1
	liftframecount = 0
	trans = 1.0
	AllocStratSoundBlock(1)
	InitHDC()
	CalculateStratPolyData(1)
	IF (type = 0)
		STAT> Test
	ENDIF
	IF (type = 1)
		tempf = liftheightmetres - (z - startz)
		tempi = TOINT(tempf / liftspeed)
		liftframecount = 0
		STAT>GoUp
	ENDIF
	IF (type = 2)
		STAT>StartNewType
	ENDIF
ENDSTATE

TRIGGER CalcCollPoly
	HoldPlayer()
	CalculateStratPolyData(1)
	TRIGFIN
ENDTRIGGER

LOCALFLOAT COUNT

STATE Test
	//HoldPlayer()
	trans = 1.0
	MyFlag = MyFlag & LNOT (STRATCOLL)
	z = startz
	ResetOCP()
	liftframecount = 0	
	MyFlag = MyFlag | NODISP

	IF (MPPlayerNearActivation(0))	

		MyFlag = MyFlag | STRATCOLL

		MyFlag = MyFlag & LNOT(NODISP)
		COUNT = liftheightframes
		LOOP (15)
			MyFlag = MyFlag
		ENDLOOP

		PLAYSOUND>LIFTLOOP 0 1.0 0.0 0.0 0.0 CONTINUOUS
		WHILE ((COUNT > 50.0) AND (MPPlayerNearActivationXY(0)))
			IF (liftframecount < liftheightframes)

				z = z + liftspeed
				liftframecount = liftframecount + 1.0

				IF ((liftheightframes - liftframecount) < 50.0)
					trans = trans - 0.02
   				ENDIF

			ENDIF
			COUNT = COUNT - 1.0
		ENDWHILE

		COUNT = 50

		WHILE (COUNT > 0.0)
			SetVolume(0, trans)
			IF (liftframecount < liftheightframes)

				z = z + liftspeed
				liftframecount = liftframecount + 1.0

				trans = trans - 0.02

			ENDIF
			COUNT = COUNT - 1.0
		ENDWHILE
		StopSound(0,0)
	ENDIF
ENDSTATE

STATE StartNewType
	IF (MPPlayerNearActivationXY(0))
		IF(z < startz + liftheightmetres)
			z = z + liftspeed
		ENDIF
	ELSE
		IF(z > startz)
			z = z - liftspeed
		ENDIF
	ENDIF
ENDSTATE

STATE GoUp
	heightAdd = SmoothFromTo(tempf, liftframecount, tempi)
	z = z + heightAdd
	liftframecount = liftframecount + 1
	IF (liftframecount = tempi)
		z = startz + liftheightmetres
		LOOP (delayTime)
			MyFlag = MyFlag
		ENDLOOP
		liftframecount = 0
		tempi = TOINT(liftheightmetres / liftspeed)
		STAT>GoDown
	ENDIF
ENDSTATE

STATE GoDown
	heightAdd = -SmoothFromTo(liftheightmetres, liftframecount, tempi)
	z = z + heightAdd
	liftframecount = liftframecount + 1
	IF (liftframecount = tempi)
		z = startz
		LOOP (delayTime)
			MyFlag = MyFlag
		ENDLOOP
		tempf = liftheightmetres - (z - startz)
		tempi = TOINT(tempf / liftspeed)
		liftframecount = 0
		STAT>GoUp
	ENDIF

	pn = 0

	IF (liftheightmetres - (z - startz) > liftspeed)
		IMMLOOP (4)
			GetPlayerPos(pn)
			IF (z > CheckZ)
				IF (z - 3.0 < CheckZ)
					IF (NearCheckPosXY(5.0))
						tempf = liftheightmetres - (z - startz)
						tempi = TOINT(tempf / liftspeed)
						liftframecount = 0
						STAT>GoUp
					ENDIF
				ENDIF
			ENDIF
			pn = pn + 1
		IMMENDLOOP
	ENDIF
ENDSTATE