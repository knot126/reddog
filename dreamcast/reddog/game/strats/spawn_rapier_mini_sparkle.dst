// Sparkly thing that hangs off the front of the mini rapiers
LOCALFLOAT spin1
LOCALFLOAT spin2
LOCALINT	lightID

STATE Init
	// Ensure parent runs first
	MakeMeTheLast()
	MyFlag = MyFlag | DELETECALLBACK
	MyFlag2 = MyFlag2 | TRANSLUCENT
	OBJECT>SFX\MINIRAPIERSPARKLE
	TRIGSET>Stick WHENPAUSED
	TRIGSET>KillOff PARENTDEAD
	TRIGSET>CleanUp WHENDELETED
	TRIGSET>Always EVERY 1
	// Create a light which is deleted on deletion by the callback
	lightID = MakePointLight (0.65, 0.65, 1.1, 30, 1.5, 0,1,0)
	STAT>Run
ENDSTATE

STATE Stick
		LinkToParentObject (10, 0, 0, 0, 0, 1.4, -0.25)
	TRIGFIN
ENDSTATE

TRIGGER Always
	IF (MyParentHealth <= 0.0)
		Delete()
	 //	TRIGSTOP
	ENDIF
ENDTRIGGER



STATE Run
	WHILE (ParentSemaphore != 0.0)
	   //	LinkToParentObject (9, 1.1, 0, -1.5, 0, 1.3, 110)
		LinkToParentObject (10, 0, 0, 0, 0, 1.4, -0.25)
	   
	   	trans = RandRange(0.3, 0.8)

		spin1 = spin1 + RandRange(-0.5, 0.5)
   		RotateObjectXYZ (1, 0, spin1, 0)
		MyVar = RandRange(0.6, 1.4)
   	  	SetObjectScale(1, MyVar, MyVar, MyVar)

		spin2 = spin2 + RandRange(-0.5, 0.5)
	 	RotateObjectXYZ (2, 0, spin2, 0)
	 
		MyVar = RandRange(0.8, 1.2)
	 	SetObjectScale(2, MyVar, MyVar, MyVar)

		MyVar = RandRange(0.5, 1.5)
		SetPointLightRadius (lightID, 20, MyVar)

	ENDWHILE
	Delete()
ENDSTATE

STATE KillOff
	PauseTriggers(-1,FORGET)
	Delete()
ENDSTATE

STATE CleanUp
	// Hoorah - clean up the light
	RemoveLight (lightID)
ENDSTATE
