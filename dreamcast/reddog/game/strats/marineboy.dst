//PAL FRIENDLY


LOCALFLOAT FireChance
LOCALINT	isGenerated

DEFINE GUNARM 3
DEFINE GUNARM2 5
DEFINE ARMLENGTH 1.0
DEFINE CHEST 7
DEFINE RUNSPEED	0.028

DEFINE CLOSERANGEFIRE 1


LOCALINT FIRED
LOCALINT TARGETVALID
LOCALINT STATUS

DEFINE NTSC_FIREDELAY 15
DEFINE PAL_FIREDELAY 12
LOCALINT FIREDELAY

STATE Init 
	IF (GetGlobalParamInt(0))
		STAT>Bazooka
	ENDIF

	STATUS = 0
 
 	IF (PAL)
		FIREDELAY = PAL_FIREDELAY
 	ELSE
		FIREDELAY = NTSC_FIREDELAY
 	ENDIF

	AllocStratSoundBlock(3)
	RegisterDanger(100)
	  
 	SetFriction(1.0,0.1,0.0)
	RotationRestriction(0.0, 0.0, 1.0)
	health = 80.0
	FIRED = 0
	isGenerated = 0
	InitPath()
   	MyFlag = MyFlag | IMPACT | STRATCOLL | ENEMY | LOWCOLL | HOVERMOVE | GRAVITYON | FLOORCOLL | TARGETABLE
	MyFlag2 = MyFlag2 | COLLMOVE
   	OBJECT> ENEMIES\Marineboy
	//EL DODGO HACK FOR SOFT COLLISION
	mass = 4001.0

	RegisterCollision()
	TRIGSET>SpecularMadness EVERY 1
	InActivateObjectCollision(CHEST)
	MODELANIM>ENEMIES\Marineboy.RDO>Walk>REPEAT

	TARGETVALID = 0
	SetObjectShadowState(1)
	
	IF (GetGlobalParamFloat(0) = 1.0)
		isGenerated = 1
		InheritMyParent()
	ELSE
		isGenerated = 0
		IF (MyParent)
			WHILE (MyParentInvalid())
				MyFlag = MyFlag
			ENDWHILE
			MyTarget = MyParent

			hitdist = 0 
			TRIGSET>TargetCheck EVERY 1
			TRIGSET>DeleteTarg  PARENTDEAD
			TRIGSET>ChangeTarg  WHENHIT
			TARGETVALID = 1
		ENDIF
	ENDIF

	IF (MyPath)
		TRIGSET>KillCheck EVERY 1 

		STAT>Move
	ELSE
		STAT>DropToFloor
	ENDIF
ENDSTATE
LOCALFLOAT playdist
LOCALFLOAT targetdist


STATE TargetCheck

	IF (!hitdist)
		SetPlayerToTarget(TARGETVALID)
		targetdist = PlayerDistXY()
  		SetPlayerBack()
		playdist = PlayerDistXY()
		
		IF (playdist < targetdist)
		  	TARGETVALID = 0
			WHILE ((playdist < targetdist))
				SetPlayerToTarget(1)
				targetdist = PlayerDistXY()
  				SetPlayerBack()
				playdist = PlayerDistXY()
			ENDWHILE
		ENDIF
	ENDIF

	TARGETVALID = 1
	TRIGFIN


ENDSTATE


//STATE TargetCheck

//	IF (!hitdist)
//	IF (NearPlayerXY(40.0))
//		WHILE (NearPlayerXY( 50.0 ))
//			TARGETVALID = 0
//	   	ENDWHILE
//		TARGETVALID = 1
//	ELSE				
//		TARGETVALID = 1
//	ENDIF
//	ENDIF

//	TRIGFIN


//ENDSTATE

LOCALFLOAT hitdist

//ENSURES TARGET IS IGNORED WHEN TANK IS SHOT BY PLAYER
STATE ChangeTarg
	hitdist = PlayerDistXY()

	WHILE (NearPlayerXY(50.0))
		TARGETVALID = 0

	ENDWHILE


	TARGETVALID = 1

	hitdist = 0
	TRIGFIN


ENDSTATE

//ENSURES THE STRAT'S TARGET IS THE PLAYER ONCE THE ORIGINAL IS DEAD
STATE DeleteTarg
	TRIGKILL>TargetCheck EVERY 1
	TRIGKILL>ChangeTarg WHENHIT
	TARGETVALID = 0
	MyTarget = Player
	TRIGSTOP
ENDSTATE



LOCALFLOAT SPECAMOUNT

TRIGGER SpecularMadness
	UpdateTrigFlag(TRIG_ALWAYS_RUN)
 //	IF ((!DEAD) AND (!FireSightPlayer())
 //		MyFlag = MyFlag | NODISP
 //	ELSE
 //		MyFlag = MyFlag & LNOT(NODISP)
 //	ENDIF

	IF ((MyFlag & HITSTRAT) AND (!(MyFlag2 & PLAYERHIT)) AND (health > 0.0))
		SPECAMOUNT = 1.0
		PLAYSOUND>ENEMY_HIT 2 1.0 0.0 0.0 0.0 RANDOM
	ENDIF
	IF (SPECAMOUNT > 0)
  		MyFlag2 = MyFlag2 | SPECULAR
		SetSpecularColour(0, SPECAMOUNT,SPECAMOUNT,SPECAMOUNT)
		IF (PAL)
			SPECAMOUNT = SPECAMOUNT - (0.1 * 1.2)
		ELSE
			SPECAMOUNT = SPECAMOUNT - 0.1
		ENDIF
   	ELSE
		SPECAMOUNT = 0
 		MyFlag2 = MyFlag2 & LNOT(SPECULAR)
	ENDIF
	TRIGFIN
ENDTRIGGER

LOCALINT thisFrame

STATE Move
	SetPlayerToTarget(TARGETVALID)

	thisFrame = MyAnimFrame
	IF (thisFrame = 11)
		PLAYSOUND>BAZOOKA_STEPL 0 0.8 0.0 0.0 0.0 0
	ENDIF
	IF (thisFrame = 23)
		PLAYSOUND>BAZOOKA_STEPR 0 0.8 0.0 0.0 0.0 0
	ENDIF

	IF( (!MyPath) OR ((!TARGETVALID) && NearPlayer(40.0)))
   		MyFlag = MyFlag & LNOT(GRAVITYON)
		MyFlag = MyFlag & LNOT(FLOORCOLL)
		SetFriction(1.0,0.4,1.0)
   		SetPlayerBack()
		MODELANIM>ENEMIES\Marineboy.RDO>Stand>0
		KillMyVelocity()
	  	STAT>Wait
	ENDIF

	IF (SeeWayZ(0.1))
			IF (!(MyAnimFlag & TWEEN))
  				MoveY(RUNSPEED/2.0)
			ENDIF
		   	IF (!(MyAnimFlag & RUNNING))
				MODELANIM>ENEMIES\Marineboy.RDO>Walk>REPEAT
		   	ENDIF
	ENDIF
	RelTurnTowardCheckPosXY(0.05)
	IF(NearCheckPosXY(2.0))
		IF(LastWay())
			MyFlag = MyFlag & LNOT(GRAVITYON)
			MyFlag = MyFlag & LNOT(FLOORCOLL)
			SetFriction(1.0,0.4,1.0)
		  	KillMyVelocity()
			MODELANIM>ENEMIES\Marineboy.RDO>Stand>0
		 	SetPlayerBack()

			STAT> Wait
		ENDIF
		GetNextWay()
	ENDIF
   	SetPlayerBack()

ENDSTATE

STATE DropToFloor
	
	WHILE (!(MyFlag & HITFLOOR))
	   	TowardPlayer(0.1,0.0)
	//	MyFlag = MyFlag
	ENDWHILE
   	TRIGSET>KillCheck EVERY 1 
   	MyFlag = MyFlag & LNOT(GRAVITYON)
   	MyFlag = MyFlag & LNOT(FLOORCOLL)
   	SetFriction(1.0,0.4,1.0)

	
   //	SetFriction(1.0,0.4,1.0)
	MODELANIM>ENEMIES\Marineboy.RDO>Stand>0
	STAT> Wait
ENDSTATE

LOCALINT Seen
STATE Wait
	SetPlayerToTarget(TARGETVALID)

  	RelTurnTowardPlayerXY(0.05)	
	Seen = NearPlayer(100.0)
	SetPlayerBack()


	IF (Seen)
		FireChance = RandRange (0, 100)

		//FUNCTION OF DISTANCE TO PLAYER CLOSER MORE FIRING 

		IF (FireChance < 3.0)
				SetPlayerToTarget(TARGETVALID)

			   	Seen = SeePlayerZ(0.05) AND (FireSightPlayer())
			  	WHILE (!Seen)
					SetPlayerToTarget(TARGETVALID)
					TowardPlayer(0.1,0.0)
					Seen = SeePlayerZ(0.05) AND (FireSightPlayer())
					SetPlayerBack()
				ENDWHILE
			   
			   
				SetPlayerToTarget(TARGETVALID)
			  	Seen = NearPlayer(25.0) AND FireSightPlayer()
			   	SetPlayerBack()
				IF (!Seen)

				
				    STATUS = STATUS & LNOT(CLOSERANGEFIRE)
				   	MODELANIM>ENEMIES\Marineboy.RDO>Fire>0
				
				 //WE AWAIT THE TWEEN TO STOP
					WHILE (MyAnimFlag & TWEEN)
						SetPlayerToTarget(TARGETVALID)
						TowardPlayer(0.1,0.0)
						SetPlayerBack()
				   	ENDWHILE


		 			LOOP (FIREDELAY)
						SetPlayerToTarget(TARGETVALID)
						TowardPlayer(0.1,0.0)
						SetPlayerBack()
					ENDLOOP					

 					PLAYSOUND>MARINE_GUN 1 1.0 0.0 0.0 0.0 0

					SetGlobalParamInt(0,1)
					SetGlobalParamInt(1,TARGETVALID)
					CREATE Marineboy 0,0,0,0,0,0, GUNARM
					CREATE Marineboy 0,0,0,0,0,0, GUNARM2
					ClearGlobalParams()
				 

					WHILE  (MyAnimFlag & RUNNING)
						MyFlag = MyFlag
					ENDWHILE
			   
			   		
			   
					MODELANIM>ENEMIES\Marineboy.RDO>Stand>REPEAT
			   

					FIRED = 0
				ELSE

					STATUS = STATUS | CLOSERANGEFIRE


					MODELANIM>ENEMIES\Marineboy.RDO>Fire>0
				 //WE AWAIT THE TWEEN TO STOP
					WHILE (MyAnimFlag & TWEEN)
						SetPlayerToTarget(TARGETVALID)
						TowardPlayer(0.1,0.0)
						SetPlayerBack()
			   		ENDWHILE


		 			LOOP (FIREDELAY)
						SetPlayerToTarget(TARGETVALID)
						TowardPlayer(0.1,0.0)
						SetPlayerBack()
					ENDLOOP					

 					PLAYSOUND>MARINE_GUN 1 1.0 0.0 0.0 0.0 0

					SetGlobalParamInt(0,1)
					SetGlobalParamInt(1,TARGETVALID)
					CREATE Marineboy 0,0,0,0,0,0, GUNARM
					CREATE Marineboy 0,0,0,0,0,0, GUNARM2
					ClearGlobalParams()
				  

					WHILE  (MyAnimFlag & RUNNING)
						SetPlayerToTarget(TARGETVALID)
						TowardPlayer(0.1,0.0)
						SetPlayerBack()
					ENDWHILE
			   
			   		
			   
				 	MODELANIM>ENEMIES\Marineboy.RDO>Stand>0
			   

				ENDIF
			
			
			

		ENDIF 
	ENDIF

ENDSTATE


LOCALFLOAT deathvel
   
STATE KillCheck

    //ensure that this trigger can never be paused	

	UpdateTrigFlag(TRIG_ALWAYS_RUN)

	IF (!FireSightPlayer())
		MyFlag = MyFlag | NODISP
	ELSE
		MyFlag = MyFlag & LNOT(NODISP)
	ENDIF

	IF ((MyFlag2 & BIGDAMAGE) || (MyFlag2 & PLAYERHIT) || (health <= 0.0))

		PauseTriggers(-1,FORGET)
		MyFlag = MyFlag & LNOT(NODISP)
		//ONE HIT DEAL SO ENSURE THE HEALTH IS ZERO'D
		health = 0.0

		IF (MyFlag2 & PLAYERHIT)
			PLAYSOUND>ENEMY_HIT3 2 1.0 0.0 0.0 0.0 RANDOM
			deathvel = FABS(PlayerRelVelY) * 1.2
			//ENSURE CORRECT FRICTION
		  	SetFriction(0.4,0.1,0.5)
		  //	SetFriction(1.0,0.4,1.0)
	  		MoveY(-deathvel)
		 //	MoveZ(3.5)
		ENDIF

		MyFlag = MyFlag & LNOT(STRATCOLL)

//		adrelanin = adrelanin + 0.2
		TRIGRESET
		TRIGSTOP
		Score = Score + 2000
		STAT>WaitForDeath
	ENDIF
	TRIGFIN
ENDSTATE

LOCALINT DEAD
LOCALFLOAT OLDHEALTH

LOCALINT time
LOCALINT DEATHTIME

STATE WaitForDeath
	IF ((MyFlag2 & BIGDAMAGE) OR (MyFlag2 & PLAYERHIT))
	  	MODELANIM>ENEMIES\marineboy.RDO>BigDeath>NOTWEEN 
		MyFlag = MyFlag | REALGRAVITYON | FLOORCOLL
	 	InActivateObjectCollision(0)
	   	ActivateObjectCollision(CHEST)
		time = 0
		//WHILE WE'RE DYING AND NOT HITTING, CONTINUE
		IF (PAL)
			DEATHTIME = 20
		ELSE
			DEATHTIME = 22
		ENDIF
		
		WHILE ((time < DEATHTIME) AND (!ObjectHitFloor(CHEST))) 
			time = time + 1
		ENDWHILE

		PLAYSOUND>BAZOOKA_FALL2 0 1.0 0.0 0.0 0.0 RANDOM

	   //	WHILE ((MyAnimFlag & RUNNING) AND (!ObjectHitFloor(GUNARM))) 
	   //		time = time + 1
	   //	ENDWHILE

		MyAnimFlag = MyAnimFlag | PAUSED

		WHILE (!ObjectHitFloor(CHEST))
 		    z = z - 1.1
		ENDWHILE

		PLAYSOUND>BAZOOKA_FALL2 0 1.0 0.0 0.0 0.0 RANDOM

		MyAnimFlag = MyAnimFlag & LNOT(PAUSED)

		WHILE (MyAnimFlag & RUNNING)
			MyFlag = MyFlag
		ENDWHILE

		MakeFrags (1.2, 10)
	  //	speedz = - speedz/2.0
	  	SetFriction(0.1,0.1,0.0)
	  	MyFlag = MyFlag & LNOT(GRAVITYON | FLOORCOLL | REALGRAVITYON)
	ELSE

		MODELANIM>ENEMIES\Marineboy.RDO>Die>NOTWEEN 



		WHILE (MyAnimFlag & RUNNING)
			
			thisFrame = MyAnimFrame
			IF ((thisFrame = 170) OR (thisFrame = 211))
				PLAYSOUND>BAZOOKA_FALL2 0 1.0 0.0 0.0 0.0 RANDOM
			ENDIF

			MyFlag = MyFlag
		ENDWHILE
	ENDIF
	
	//FADE OUT THE FUCKWIT
	MyFlag2 = MyFlag2 | TRANSLUCENT
	WHILE (trans > 0.02)
		trans = trans - 0.02
	ENDWHILE
	IF (isGenerated)
		ParentVar = ParentVar + 1.0
	ENDIF
	Delete ()
ENDSTATE

LOCALFLOAT stretch
LOCALINT   frames


STATE Bazooka
	IF (GetGlobalParamInt(1))
		TARGETVALID = 1
	ENDIF

	AllocStratSoundBlock(2)

	OBJECT>Weapon\FootSoldierLaser
	ExplosionLight (0.1, 0.1, 0.25, 13.5, 1.0, 1)
 	SetFriction(0.0,0.0,0.0)
	damage = 6.0
	MakeMeTheLast()
	TRIGSET>DoTail EVERY 1

	TRIGSET>HitStrat WHENHIT
	TRIGSET>KillOff	ONFLOOR

	//ALLOW LITTLE OVER 3 SECS TO BE ALIVE
	IF (PAL)
		TRIGSET>KillOff	EVERY 80
	ELSE
		TRIGSET>KillOff	EVERY 100
	ENDIF


	MyFlag = MyFlag  | STRATCOLL | NODISP | ENEMY | NOTEXPLODE | BULLETTYPE | LOWCOLL | FLOORCOLL
	MyFlag2 = MyFlag2 | BULLET_CF
	RegisterCollision()

	MyTarget = MyParentTarget
	IF ((MyTarget) AND (TARGETVALID))
		CheckX = TargetX
		CheckY = TargetY
		CheckZ = TargetZ
	   	PointToTarget()
   		TRIGSET>KillOff WHENTARGETDEAD
   	ELSE
		//IF THE PLAYER IS ON THE TRAIN TARGET A POINT AHEAD OF HIM
		//BY SPEED

		IF ((PlayerState = PS_ONTRAIN) OR (PlayerState = PS_WATERSLIDE))
			health = PlayerActVelX * PlayerActVelX
			MyVar = PlayerActVelY * PlayerActVelY
			frame = PlayerActVelZ * PlayerActVelZ

			frame = FSQRT(health + MyVar + frame)
			health = PlayerActVelX / frame
			MyVar = (PlayerActVelY / frame) * 3.0
			frame = PlayerActVelZ / frame
		  	SetCheckPosRelOffsetPlayerObject(0,health,MyVar, frame)
		
			PointToTarget()
		ELSE
			PointPlayer ()
		ENDIF
	ENDIF
  	MoveY(0.1)

	frames = 0
	STAT>SetPos
ENDSTATE

STATE DoTail
	FaceAlongDir()
	TRIGFIN
ENDSTATE

LOCALFLOAT offSx
LOCALFLOAT offSy

STATE SetPos
	IF ((MyTarget) AND (TARGETVALID))
		PointToTarget()
	ELSE
		IF ((PlayerState = PS_ONTRAIN) OR (PlayerState = PS_WATERSLIDE))
			offSx = 5.0 * RandRange(0.15,0.25)
			offSy = 5.0 * RandRange(0.15,0.25)
			SetCheckPosRelOffsetPlayerObject(0,offSx, offSy, 0)
			PointToTarget()
		ELSE
			PointPlayer ()
		ENDIF
	ENDIF
	MoveY(1.5)
	PLAYSOUND>WALKER_FIREBALL 0 0.9 0.0 0.0 0.0 CONTINUOUS
	TRIGSET>JonnyBazooka EVERY 2
	STAT>MoveMe
ENDSTATE

STATE MoveMe
	IF ((PlayerState = PS_ONTRAIN) OR (PlayerState = PS_WATERSLIDE))
   	   	x = x + PlayerActVelX
   	 	y = y + PlayerActVelY
   	 	z = z + PlayerActVelZ
   	   TowardPlayer(0.05,0.05)	
	ENDIF
ENDSTATE

STATE KillOff
	KillMyVelocity()
	MyVar = 10.5
   	CREATE SPAWN_BLASTEXP 0,0,0,0,0,0,0

	Delete()
ENDSTATE

TRIGGER HitStrat
	IF (CollWith = PlayerShieldStrat(0))
		PLAYSOUND>DAMAGE3 1 1.0  0.0 0.0 0.0  0
		MyFlag = MyFlag | FRIENDLY
		BounceOffShield(0)
		TRIGFIN
	ELSE
		KillMyVelocity()
		MyVar = 10.5
   		CREATE SPAWN_BLASTEXP 0,0,0,0,0,0,0

		Delete()
		TRIGSTOP
	ENDIF
ENDTRIGGER


TRIGGER JonnyBazooka
	MyVar = 4.5
	SetGlobalParamInt(0,99)
	SetGlobalParamFloat(0,0.3)
	CREATE SPAWN_BLASTEXP 0,0,0,0,0,0,0
	ClearGlobalParams()

	TRIGFIN
ENDTRIGGER

