PARAMINT	aliveTime	1000
PARAMINT	type		0				// 0 is the normal electric badger, 1 is the fast train badger
PARAMINT	holdfiretime	0
PARAMFLOAT	fastspeed	1.6

DEFINE		REVERSE		1
DEFINE		FORWARD		2
DEFINE		ENGAGED		4
DEFINE		ACC			0.1
DEFINE		STOPRANGE	50
DEFINE		REVERSERANGE	30
DEFINE		VERT_FIRE		2.3
DEFINE		HOR_FIRE		4.0
DEFINE		FOR_FIRE		3.0
DEFINE		ELECTRIC		0
DEFINE		FAST			1

LOCALFLOAT  velocity
LOCALFLOAT  xang
LOCALFLOAT  yang
LOCALFLOAT  zang
LOCALFLOAT  rand
LOCALFLOAT  randFireX
LOCALFLOAT  randFireZ
LOCALFLOAT	tx
LOCALFLOAT	ty
LOCALFLOAT	tz
LOCALFLOAT	ox
LOCALFLOAT	oy
LOCALFLOAT	oz
LOCALFLOAT	rx
LOCALFLOAT	ry
LOCALFLOAT	rz

LOCALINT	Mode
LOCALINT	time
LOCALINT	Lightning
LOCALINT	shockHim
LOCALINT	shockTime
LOCALINT	startfiretime 

//STATE Init
//	Delete()
//ENDSTATE


STATE Init
	//ENSURE WE HAVE THE NEW MODEL
	OBJECT>ENEMIES\HOVERBOT_LEVEL2
	IF (!(MyFlag2 & OWNIPARAMS))
		aliveTime = 1000
		holdfiretime = 0
	   	type = 0
	ENDIF
	IF (!(MyFlag & OWNFPARAMS))
		fastspeed = 1.6
	ENDIF

	AllocStratSoundBlock(2)
	RegisterDanger(100.0)
	tx = RandRange(-0.2, 0.2)
	SetPitch(0, tx) 
	PLAYSOUND>HOVERBOT_LOOP3 0 0.8 0.0 0.0 0.0 CONTINUOUS

	IF (type = ELECTRIC)
		STAT>HoverBotElectricInit
	ENDIF
	IF (type = FAST)
		STAT>HoverBotFastInit
	ENDIF
	
ENDSTATE


STATE HoverBotElectricInit
	MyFlag = MyFlag | STRATCOLL | TARGETABLE | LOWCOLL | ENEMY
	RegisterCollision()
	MyVar = 1
	IF (aliveTime = 0)
		aliveTime = 1000
	ENDIF
	SetFriction(0.05, 0.05, 0.05)
	InitPath()
	InitSplineData()
	TRIGSET>HoverBotFastExplode WHENHIT 
	TRIGSET>HoverBotElectricFiringWarez EVERY 1
	xang = RandRange(0.0, 3.1415)
	yang = RandRange(0.0, 3.1415)
	zang = RandRange(0.0, 3.1415)
	Mode = FORWARD
	time = 0
	shockHim = 0
	startfiretime = 0
	health = 1.0
	velocity = 0.2
	GlobalVar = 0.2
	AllocStratSoundBlock(1)
	SetObjectShadowState(1)
	STAT>HoverBotElectricStart
ENDSTATE

STATE HoverBotElectricStart
	IF (MyVar = 1)
		STAT> HoverBotElectricGetBusy
	ENDIF
ENDSTATE

STATE HoverBotElectricFiringWarez
	WHILE (startfiretime < holdfiretime)
		MyFlag = MyFlag
	ENDWHILE
	IF (!NearPlayerXY(100))
		shockHim = 0
		shockTime = 0
	ENDIF
	IF (shockHim = 0)
		IF (NearPlayerXY(100.0))
			IF (NearPlayerXY(100.0))
				IF (RandRange(0.0, 90.0) < 1.0)
					shockHim = 1
					shockTime = 0
				ENDIF
			ELSE
				rand = RandRange(0.0, 100.0)
				IF (rand < 1.0)
					randFireX = GlobalVar + 0.03
					randFireZ = GlobalVar + 0.03
					randFireX = RandRange(- randFireX , randFireX )
					randFireZ = RandRange(- randFireZ , randFireZ )
					PLAYSOUND>SQUAR009PMIX 1 1.0 0.0 0.0 0.0 0
//					PlaySound (0,6,1, 0, 0, 0, 0)
					CREATE SPAWN_RED_LASER_ENEMY -1.3, 0.0, -0.6, randFireX, 0, randFireZ, 0
					CREATE SPAWN_RED_LASER_ENEMY 1.3, 0.0, -0.6, randFireX, 0, randFireZ, 0
					IF (GlobalVar > 0.01)
						GlobalVar = GlobalVar - 0.01
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ELSE	
		shockTime = shockTime + 1
		IF (shockTime > 0)
			MyStratToWorld(-HOR_FIRE, FOR_FIRE, 0.0)
			tx = CheckX
			ty = CheckY
			tz = CheckZ 
			MyStratToWorld(0.0, FOR_FIRE, VERT_FIRE)
			DrawShock(tx, ty, tz, CheckX, CheckY, CheckZ, 0.1, 2.0, 2.0)
		ENDIF
			
		IF (shockTime > 20)
			MyStratToWorld(0.0, FOR_FIRE, VERT_FIRE)
			tx = CheckX
			ty = CheckY
			tz = CheckZ
			MyStratToWorld(HOR_FIRE, FOR_FIRE, 0.0)
			DrawShock(tx, ty, tz, CheckX, CheckY, CheckZ, 0.1, 2.0, 2.0)
		ENDIF

		IF (shockTime > 40)
			MyStratToWorld(HOR_FIRE, FOR_FIRE, 0.0)
			tx = CheckX
			ty = CheckY
			tz = CheckZ
			MyStratToWorld(0.0, FOR_FIRE, -VERT_FIRE)
			DrawShock(tx, ty, tz, CheckX, CheckY, CheckZ, 0.1, 2.0, 2.0)
		ENDIF

		IF (shockTime > 60)
			MyStratToWorld(0.0, FOR_FIRE, -VERT_FIRE)
			tx = CheckX
			ty = CheckY
			tz = CheckZ
			MyStratToWorld(-HOR_FIRE, FOR_FIRE, 0.0)
			DrawShock(tx, ty, tz, CheckX, CheckY, CheckZ, 0.1, 2.0, 2.0)
		ENDIF

		IF (shockTime > 80)
			MyStratToWorld(0.0, FOR_FIRE, 0.0)
			tx = CheckX
			ty = CheckY
			tz = CheckZ
			MyStratToWorld(-HOR_FIRE, FOR_FIRE, 0.0)
			DrawShock(tx, ty, tz, CheckX, CheckY, CheckZ, 0.2, 2.0, 2.0)
			MyStratToWorld(HOR_FIRE, FOR_FIRE, 0.0)
			DrawShock(tx, ty, tz, CheckX, CheckY, CheckZ, 0.2, 2.0, 2.0)
			MyStratToWorld(0.0, FOR_FIRE, VERT_FIRE)
			DrawShock(tx, ty, tz, CheckX, CheckY, CheckZ, 0.2, 2.0, 2.0)
			MyStratToWorld(0.0, FOR_FIRE, -VERT_FIRE)
			DrawShock(tx, ty, tz, CheckX, CheckY, CheckZ, 0.2, 2.0, 2.0)
		ENDIF

		IF ((shockTime > 100) AND (shockTime < 120) AND (PlayerHealth > 0.0))
			PlayerHealth = PlayerHealth - 2.0
			Lightning = DrawLightning (0, 0, 0, 0, Lightning,0)
		ENDIF

		IF (shockTime > 120)
			shockHim = 0
			shockTime = 0
		ENDIF
	ENDIF
ENDSTATE

STATE HoverBotElectricGetBusy
	time = time + 1
	IF (NearPlayer(REVERSERANGE))
		Mode = (Mode | REVERSE | ENGAGED) & LNOT(FORWARD)
		IF (velocity > -1.0)
			velocity = velocity - ACC
		ENDIF
	ELSE
		IF (!NearPlayer(STOPRANGE))
			Mode = (Mode | FORWARD) & LNOT(REVERSE)
			IF (velocity < 1.0)
				velocity = velocity + ACC
			ENDIF
		ELSE
			velocity = velocity * 0.9	
		ENDIF
	ENDIF	
	IF (velocity < 0.0)
		IF (!NearActivationXY(0))
			MoveAlongPath(velocity)
			rx = x
			ry = y
			rz = z
		ELSE
			x = rx
			y = ry
			z = rz
			x = ox
			y = oy
			z = oz
			MyFlag = MyFlag
		ENDIF
	ELSE
		MoveAlongPath(velocity)
		rx = x
		ry = y
		rz = z
	ENDIF
	FaceAlongPath(2)
	ox = x
	oy = y
	oz = z
	x = x + Ssin(xang)
	xang = xang + 0.07
	y = y + Ssin(yang)
	yang = yang + 0.07
	z = z + Ssin(zang)
	zang = zang + 0.1
	PointToXY(DogX, DogY, DogZ)
	IF (Mode & ENGAGED)	
		time = time + 1
		IF (time > 1000)
			STAT>HoverBotElectricLegit
		ENDIF 
	ENDIF
ENDSTATE

STATE HoverBotElectricExplode
	MyVar = 0.0
	CREATE SPAWN_BLASTEXP 0, 0, 0, 0, 0, 0, 0
	Score = Score + 100
//	adrelanin = adrelanin + 0.1
	Delete()
 //	TRIGSTOP
ENDSTATE

STATE HoverBotElectricLegit
	MoveZ(0.1)
	shockTime = 0 
	time = time + 1
	IF (time > aliveTime)
		Delete()
	ENDIF
ENDSTATE

STATE HoverBotFastInit
	MyFlag = MyFlag | ENEMY | STRATCOLL | LOWCOLL
	RegisterCollision()
 	WHILE (!PlayerNearActivationXY(0))
		MyFlag = MyFlag
	ENDWHILE
	xang = RandRange(0.0, 3.1415)
	yang = RandRange(0.0, 3.1415)
	zang = RandRange(0.0, 3.1415)
	InitPath()
	TRIGSET>HoverBotElectricFiringWarez EVERY 1
	health = 1.0
	InitSplineData()
	GlobalVar = 0.2
	TRIGSET>HoverBotElectricExplode WHENDEAD 
	SetObjectShadowState(1)
	STAT>HoverBotFastStart
ENDSTATE

STATE HoverBotFastStart
	MoveAlongPath(fastspeed)
	x = x + Ssin(xang)
	xang = xang + 0.07
	y = y + Ssin(yang)
	yang = yang + 0.07
	z = z + Ssin(zang)
	zang = zang + 0.1
	PointToXY(DogX, DogY, DogZ)
   
	
	IF (LastWay())
		STAT>HoverBotFastLegit		
	ENDIF
ENDSTATE

STATE HoverBotFastExplode
//	adrelanin = adrelanin + 0.1
	Score = Score + 100
	CREATE SPAWN_BLASTEXP 0, 0, 0, 0, 0, 0, 0
	Delete()
 //	TRIGSTOP
ENDSTATE

STATE HoverBotFastLegit
	LOOP (60)
		shockTime = 0 
		MoveZ(0.1)
	ENDLOOP
	Delete()
ENDSTATE
