LOCALINT	closeWings
LOCALINT	closeWingsTime
LOCALINT	openWings
LOCALINT	openWingsTime
LOCALINT	fireTime
LOCALINT	attackMode
LOCALFLOAT	time
LOCALFLOAT	tempf
LOCALFLOAT	ax
LOCALFLOAT	ay
LOCALFLOAT	az
LOCALFLOAT	yrot
LOCALFLOAT	yrotVel
LOCALFLOAT	speed
LOCALFLOAT	barAlpha
LOCALFLOAT	oldHealth
LOCALFLOAT	startHealth

DEFINE		PHASE_ONE	1
DEFINE		PHASE_TWO	2
DEFINE		PHASE_THREE	3
DEFINE		PHASE_FOUR	4

DEFINE		EasyHealth	3000.0
DEFINE		HardHealth	6000.0

STATE Die
	Delete()

ENDSTATE

STATE Init
	barAlpha = 0
	IF (Hard)	
		startHealth = HardHealth
	ELSE
		startHealth = EasyHealth
	ENDIF
	IF (BossStrat)
		MyFlag = NODISP 
		WHILE (BossStrat)
			MyFlag = MyFlag
		ENDWHILE
		STAT>Die
	ELSE
		MyFlag = MyFlag | ENEMY | STRATCOLL | RELVELCALC
		IAmDaBoss
		CREATE CanyonBossGuide 0, 0, 0, 0, 0, 0, 0
		RegisterCollision()
		OBJECT>BOSSES\CANYONBOSS\CANYONBOSS
		
		time = 0.0
		yrot = 0.0
		yrotVel = 0.0
		ax = 0
		ay = 0
		az = 0
		TRIGSET>SpecularMadness EVERY 1
		attackMode = PHASE_ONE
		health = startHealth * 0.25
		oldHealth = startHealth * 0.25
		fireTime = 0
		closeWingsTime = 0
		InitHDC()
		TRIGSET>Always EVERY 1
		TRIGSET>HealthBar EVERY 1
		TRIGSET>HealthBar WHENPAUSED
		SetObjectHealth(9, 50000.0)
		SetObjectHealth(10, 50000.0)
		SetObjectHealth(11, 50000.0)
		SetObjectHealth(12, 50000.0)
		SetObjectHealth(13, 300000.0)
		SetObjectHealth(14, 300000.0)
		SetObjectHealth(15, 300000.0)
		SetObjectHealth(16, 300000.0)
		SetObjectHealth(17, 50000.0)
		SetObjectHealth(18, 50000.0)
		ObjectTargetable(13)
		ObjectTargetable(14)
		ObjectTargetable(15)
		ObjectTargetable(16)
		HideObject(19)
		AllocStratSoundBlock(3)
		SetAudibleRange(0, 100, 400)
		SetAudibleRange(1, 100, 400)
		SetAudibleRange(2, 100, 400)
		PLAYSOUND>CANYONBOSS 0 0.55 0.0 0.0 0.0 CONTINUOUS
		TimeFunction(1)
		STAT>Start
	ENDIF
ENDSTATE

LOCALFLOAT SPECAMOUNT

TRIGGER SpecularMadness
	UpdateTrigFlag(TRIG_ALWAYS_RUN)
	
	IF ((MyFlag & HITSTRAT) AND (CollWithFlag & BULLETTYPE))
		SPECAMOUNT = 1.0
		PLAYSOUND>ENEMY_HIT2 1 1.0 0.0 0.0 0.0 RANDOM
	ENDIF
	IF (SPECAMOUNT > 0)
  		MyFlag2 = MyFlag2 | SPECULAR
		SetSpecularColour(0, SPECAMOUNT,SPECAMOUNT,SPECAMOUNT)
		IF (PAL)
			SPECAMOUNT = SPECAMOUNT - 0.12
		ELSE
			SPECAMOUNT = SPECAMOUNT - 0.1
		ENDIF
	ELSE
		SPECAMOUNT = 0
 		MyFlag2 = MyFlag2 & LNOT(SPECULAR)
	ENDIF
	TRIGFIN
ENDTRIGGER

TRIGGER HealthBar
	IF (!NearPlayerXY(100))
		health = oldHealth
	ENDIF
	oldHealth = health

	barAlpha = barAlpha + ((1 - barAlpha) * 0.1)
	IF (attackMode = PHASE_ONE)
		DrawStratBar(1, (health + startHealth * 0.75) / startHealth, 0, barAlpha)
	ENDIF
	IF (attackMode = PHASE_TWO)
		DrawStratBar(1, (health + startHealth * 0.5) / startHealth, 0, barAlpha)
	ENDIF
	IF (attackMode = PHASE_THREE)
		DrawStratBar(1, (health + startHealth * 0.25) / startHealth, 0, barAlpha)
	ENDIF
	IF (attackMode = PHASE_FOUR)
		DrawStratBar(1, (health / startHealth), 0, barAlpha)
	ENDIF
	TRIGFIN
ENDTRIGGER

TRIGGER Always
	IF(GetSem(0) = 1)
		fireTime = 0
		IF (!NearPlayerXY(400))
			yrot = 0.0
			yrotVel = 0.0
			attackMode = attackMode + 1
			health = startHealth * 0.25
			oldHealth = startHealth * 0.25
			SetSem(0, 0)
		ELSE
			health = 0
		ENDIF
	ELSE
		IF (health <= 0.0)
			SetSem(0, 1)
			closeWingsTime = 0
		ENDIF
	ENDIF

	IF (NearPlayerXY(100))
		fireTime = fireTime + 1
	ELSE
		
		openWingsTime = 0
	ENDIF

	IF (closeWingsTime < 30)
		closeWingsTime = closeWingsTime + 1
		tempf = SmoothFromToSum(5.0, closeWingsTime, 30.0)
		SetObjectCrntPos(1, tempf, MODEX)
		SetObjectCrntPos(2, tempf, MODEX)
		SetObjectCrntPos(3, tempf, MODEX)
		SetObjectCrntPos(4, tempf, MODEX)
	ENDIF

	IF (openWingsTime < 30)
		openWingsTime = openWingsTime + 1
		tempf = 5.0 - SmoothFromToSum(5.0, openWingsTime, 30.0)
		SetObjectCrntPos(1, tempf, MODEX)
		SetObjectCrntPos(2, tempf, MODEX)
		SetObjectCrntPos(3, tempf, MODEX)
		SetObjectCrntPos(4, tempf, MODEX)
	ENDIF

	IF (attackMode = PHASE_ONE)
		IF ((fireTime = 10) || (fireTime = 20) || (fireTime = 30) || (fireTime = 40))
				SetGlobalParamInt(0, -1)
				CREATE SPAWN_CanyonBossMISSILE 0, 0, 0, 0, 0, 0, 13
				SetGlobalParamInt(0, 0)
				CREATE SPAWN_CanyonBossMISSILE 0, 0, 0, 0, 0, 0, 14
				CREATE SPAWN_CanyonBossMISSILE 0, 0, 0, 0, 0, 0, 15
				CREATE SPAWN_CanyonBossMISSILE 0, 0, 0, 0, 0, 0, 16
		ENDIF		
		IF (fireTime = 100)
			fireTime = 0
		ENDIF
	ENDIF

	IF (attackMode = PHASE_TWO)
		IF ((fireTime = 30) || (fireTime = 90))
			SetGlobalParamInt(0, -1)
			CREATE SPAWN_CanyonBossHomingMissile 0, 0, 0, 0, 0, 0, 9
			SetGlobalParamInt(0, 0)
		ENDIF
		IF ((fireTime = 60) || (fireTime = 120))
			SetGlobalParamInt(0, 1)
			CREATE SPAWN_CanyonBossHomingMissile 0, 0, 0, 0, 0, 0, 10
			SetGlobalParamInt(0, 0)
		ENDIF
		IF (fireTime = 120)
			fireTime = 0
		ENDIF
	ENDIF

	IF (NearPlayerXY(100.0))
		IF (attackMode = PHASE_THREE)
			IF (fireTime < 30)
				IF (yrotVel < 0.1)
					yrotVel = yrotVel + 0.005
				ENDIF
			ENDIF
			
			
			IF (fireTime = 30)
				CREATE SPAWN_CanyonBossEnergyBallGuide 0, 0, 0, 0, 0, 0, 18
				fireTime = 0
			ENDIF
			yrot = yrot + yrotVel
		ENDIF
	ELSE
		IF (yrotVel > 0.0)
			yrotVel = yrotVel - 0.005
			yrot = yrot + yrotVel
		ENDIF
	ENDIF

	IF (attackMode = PHASE_FOUR)
		IF (fireTime > 1)
			IF (!IsPlaying(2))
				PLAYSOUND>CANYONBEAM2 2 1.0 0.0 0.0 0.0 CONTINUOUS
			ENDIF
			UnhideObject(19)
			DrawLensFlare(19, 0, 0, 0, 0, 1.0, 0.0, 1.0)
			CREATE SPAWN_CanyonBossBeam 0, 0, 0, 0, 0, 0, 0
			IF (InfrontOfObject(18, 200.0) < 2.0)
				PlayerHealth = PlayerHealth - 5.0
			ENDIF
		ELSE
			IF (IsPlaying(2))
				StopSound(2,0.0)
			ENDIF
			HideObject(19)
		ENDIF
	ENDIF

	speed = StratSpeed()
	SetPitch(0, speed)

	TRIGFIN
ENDTRIGGER

STATE Start
	time = time + 0.1
	ax = ((BossGuideX - x) * 0.01)
	ay = ((BossGuideY - y) * 0.01)
	az = ((BossGuideZ - z) * 0.01)
	absspeedx = (absspeedx + ax) * 0.9
	absspeedy = (absspeedy + ay) * 0.9
	absspeedz = (absspeedz + az) * 0.9
	FaceAlongDir()
	tempf = Ssin(time * 0.5) * 0.3
	RelRotateY(tempf + yrot)
	CalculateStratPolyData(0)

	IF ((attackMode = PHASE_FOUR) AND (health <= 0.0))
		STAT>Destroy
	ENDIF
ENDSTATE

STATE Destroy
	StopSound(2,0.0)
	CREATE SPAWN_BLASTEXP 0, 0, 0, 0, 0, 0, 0
	//BossStrat = 0
	Score = Score + 30000
	TimeFunction(2)
	DESTROYME SPAWN_EXPLODINGBITS
ENDSTATE