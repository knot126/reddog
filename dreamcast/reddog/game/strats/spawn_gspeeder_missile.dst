LOCALINT innerBand
LOCALINT outerBand

STATE Init
	MyFlag = MyFlag | BULLETTYPE | ENEMY | STRATCOLL | FLOORCOLL | LOWCOLL | NODISP
	MyFlag2 = MyFlag2 | BULLET_CF
  	RegisterCollision()
	AllocStratSoundBlock(1)
  	TRIGSET>Kill	EVERY 300
	TRIGSET>HitStrat WHENHIT
	TRIGSET>Explode	ONFLOOR
	TRIGSET>BandTrigger EVERY 1 
	SetFriction(0.0, 0.0, 0.0)

	OBJECT> WEAPON\MISSILE02
	damage = 10.0
	innerBand = CreateBand (1, 4, 0, 1, 1.0, 0.5, 0, 0, 0, 0, 0)
	outerBand = CreateBand (1, 4, 0, 1, 1.0, 0.0, 0.0, 0, 0, 0, 0)
	AddBandSegment (innerBand, 0, 0, 0, 0.1)
	AddBandSegment (outerBand, 0, 0, 0, 0.3)
	
	PointTo(DogX, DogY, DogZ)
	MoveY(4.0)
	IF (PlayerState = PS_ONTRAIN)
		x = x + PlayerActVelX
		y = y + PlayerActVelY
		z = z + PlayerActVelZ
		absspeedx = absspeedx + PlayerActVelX
		absspeedy = absspeedy + PlayerActVelY
		absspeedz = absspeedz + PlayerActVelZ
	ENDIF
	STAT>	Start
ENDSTATE

STATE BandTrigger
	AddBandSegment (innerBand, 0, 0, 0, 0.1)
	AddBandSegment (outerBand, 0, 0, 0, 0.3)
	TRIGFIN
ENDSTATE

STATE Start
	MyFlag = MyFlag 
ENDSTATE

STATE Explode
	MyVar = 3.0
	CREATE SPAWN_BLASTEXP 0, 0, 0, 0, 0, 0, 0
	DeleteBand (innerBand)
	DeleteBand (outerBand)
	Delete()
 //	TRIGSTOP
ENDSTATE

STATE Kill
	DeleteBand (innerBand)
	DeleteBand (outerBand)

	Delete()
 //	TRIGSTOP
ENDSTATE

TRIGGER HitStrat
	IF (CollWith = PlayerShieldStrat(0))
		PLAYSOUND>DAMAGE3 0 1.0  0.0 0.0 0.0  RANDOM
		MyFlag = MyFlag | FRIENDLY
		BounceOffShield(0)
		TRIGFIN
	ELSE
		MyVar = 3.0
		CREATE SPAWN_BLASTEXP 0, 0, 0, 0, 0, 0, 0
		DeleteBand (innerBand)
		DeleteBand (outerBand)
		Delete()
	//	TRIGSTOP
	ENDIF
	
ENDTRIGGER
