LOCALFLOAT aimTime
LOCALFLOAT	speed
LOCALFLOAT	Xoffset
LOCALFLOAT	Yoffset
LOCALFLOAT	Zoffset
LOCALFLOAT	time 
LOCALFLOAT	rotA
LOCALFLOAT	i
LOCALFLOAT	fireDir
LOCALFLOAT	dir
LOCALFLOAT tempf
LOCALFLOAT pitch
LOCALINT fireTime
LOCALINT	myi


STATE Init
	MyFlag = ENEMY | STRATCOLL | LOWCOLL | TARGETABLE
	OBJECT>ENEMIES\HOVERBOTCITY
	RegisterCollision()
   	InitPath()
	InitSplineData()
	WHILE(!PlayerNearActivationXY(0))
		MyFlag = MyFlag
	ENDWHILE
	TRIGSET>Explode WHENDEAD
	TRIGSET>Move EVERY 1
	speed = 0
	health = 1.0
	fireDir = 0.0
	dir = 1.0
	time = 0.0
	aimTime = RandRange(0.0, 100)
	i = 0.0
	rotA = 0.0
	AllocStratSoundBlock(2)
	RegisterDanger(40.0)
	pitch = -0.4
	SetPitch(0, pitch)
	PLAYSOUND>CITYBOT_MOVE 0 0.4 0.0 0.0 0.0 CONTINUOUS
	SetObjectShadowState(1)
	STAT>Start
ENDSTATE

STATE Move
	MoveAlongPath(speed * dir)
	IF (speed >= 0.05)
		FaceAlongPath(1)
	ENDIF
	time = time + 1.0
	Xoffset = Ssin(time * 0.05)
	Yoffset = Ssin(time * 0.06)
	Zoffset = Scos(time * 0.07)
	x = x + Xoffset
	y = y + Yoffset
	z = z + Zoffset
	IF (fireDir < 0.5)
		RelRotateZ(rotA)
	ELSE
		RelRotateZ(-rotA)
	ENDIF
	TRIGFIN
ENDSTATE

STATE Start
	IF (speed < 0.4)
		speed = speed + 0.05
		pitch = -0.4 + speed
		SetPitch(0, pitch)
	ENDIF

	IF (aimTime > 0.0)
		aimTime = aimTime - 1.0
	ENDIF

	IF (aimTime <= 0.0)
		aimTime = 0.0
		STAT>Fire
	ENDIF

	IF (LastSplineSection())
		IF (dir = 1.0)
			IF (distToLastWay() < 10)
				WHILE(speed > 0.05)
					speed = speed - 0.05
					pitch = -0.4 + speed
					SetPitch(0, pitch)
				ENDWHILE
				speed = 0.0
				myi = 0
				WHILE (myi < 60)
					myi = myi + 1
					tempf = SmoothFromTo(3.14, myi, 60)
					RelRotateZ(tempf)
				ENDWHILE
				dir = -1.0
			ENDIF
		ENDIF
	ENDIF

	IF (FirstSplineSection())
		IF (dir = -1.0)
			IF (distToFirstWay() < 10)
				WHILE(speed > 0.05)
					speed = speed - 0.05
					pitch = -0.4 + speed
					SetPitch(0, pitch)
				ENDWHILE
				dir = 1.0
			ENDIF
		ENDIF
	ENDIF
ENDSTATE

STATE Fire
	IF (speed > 0.0)
		speed = speed - 0.05
	ELSE
		fireDir = RandRange(0.0, 1.0)
		i = 0
		LOOP 30
			i = i + 1.0
			rotA = SmoothFromTo(1.0, i, 30.0)
		ENDLOOP
		
		fireTime = 0
		LOOP 90 
			IF (fireTime%10 = 0)
				PLAYSOUND>LASER1XTPMIX2 1 0.65 0.0 0.0 0.0 RANDOM
				CREATE SPAWN_RED_LASER_ENEMY -1.3, 0.0, -0.6, 0, 0, 0, 0
				CREATE SPAWN_RED_LASER_ENEMY 1.3, 0.0, -0.6, 0, 0, 0, 0
			ENDIF
			fireTime = fireTime + 1
		ENDLOOP
		i = 0
		LOOP 30
			i = i + 1.0
			rotA = -SmoothFromTo(1.0, i, 30.0)
		ENDLOOP
		aimTime = RandRange(60.0, 120.0)
		STAT>Start
	ENDIF
ENDSTATE

STATE Explode
//	adrelanin = adrelanin + 0.1
	Score = Score + 500
	CREATE SPAWN_BLASTEXP 0, 0, 0, 0, 0, 0, 0
	Delete()
  //	TRIGSTOP
ENDSTATE

STATE Fade
	MyFlag2 = MyFlag2 | TRANSLUCENT
	IF (trans > 0.01)
		trans = trans - 0.01
	ELSE
		Delete()
	ENDIF
ENDSTATE