LOCALINT targetNo
LOCALINT trail
LOCALFLOAT	xSpeed
LOCALFLOAT	zSpeed

LOCALINT	targetStrat
LOCALINT	pn
LOCALINT	oid
LOCALINT	parentDead

STATE Init
  	MyFlag = MyFlag | FLOORCOLL | STRATCOLL | FRIENDLY | NOTEXPLODE |  BULLETTYPE | PLANEMOVE
	MyFlag2 = MyFlag2 | BULLET_CF
	pn = GetGlobalParamInt(0)
	MyVar = pn
	parentDead = 0
	MakeMeTheLast()
	SetFriction(0.1,0.1,0.1)
	OBJECT> WEAPON\MISSILE06
	RegisterCollision()
	SetObjectShadowState(1)
	TRIGSET>KillOff	WHENHIT
	TRIGSET>KillOffFloor	ONFLOOR
	TRIGSET>KillOffFloor	EVERY 300
	TRIGSET>Always	EVERY 1
	IF (Multiplayer)
		GetPlayerColour(pn)
		damage = 20.0 
		trail = CreateBand (1, 16, 0, 1, CheckX, CheckY, CheckZ, 1, 0, 0, 0)
	ELSE
		damage = 101.0 
		trail = CreateBand (1, 16, 0, 1, 1, 1, 0, 0, 1, 0, 0)
	ENDIF
	targetStrat = GetSecondaryTargetStrat(pn)
	oid = GetSem(0)
	SetSem(0, 0)
	RemoveTargetStrat(pn, targetStrat, oid)

	SetBandRotXYZandScaling (trail, 0.0, 0, 0.2, 1.0, 1.0, 1.0)
	AddBandSegment (trail, 0, 0, 0, 0.2)
	AddBandSegment (trail, 0, 0, 0, 0.2)
	TRIGSET>Trailer EVERY 1
	TRIGSET>BandTrigger EVERY 2

	IF (targetNo = -1)
		DeleteBand (trail)
		SubParentSem(0, 1)
		Delete()
	ELSE
		ExplosionLight(0.1, 0.08, 0.08, 15, 0.98, 3)
		frame = 0.0
		STAT>Start
	ENDIF
ENDSTATE 

TRIGGER Always
	IF (!MyParentFlag)
		parentDead = 1
		TRIGSTOP
	ELSE
		TRIGFIN
	ENDIF
ENDTRIGGER

STATE BandTrigger
	AddBandSegment (trail, 0, 0, 0, 0.2)
	TRIGFIN
ENDSTATE

STATE Trailer
	RubberBand (trail, 0, 0, 0, 0.1)
	TRIGFIN
ENDSTATE

LOCALINT NOUPDATE
LOCALFLOAT SaveX
LOCALFLOAT SaveY

STATE Start
	IF (BossStrat)
		IF (uId(BossStrat) = 20)
			y = y + 2.0
		ENDIF
	ENDIF
	IF (ValidTargetStrat(pn, targetStrat, oid))
		//AM I STILL ALLOWED TO UPDATE MY POSITION ?
 		IF (!NOUPDATE)
			GetTargetStratPos(pn, targetStrat, oid)
			TargettingStrat(targetStrat, 1)
			IF (frame)
				//IF THE DISTANCE CHANGE IS TOO GREAT, SET THE MISSILE
				//TO 'LOCK OFF'
				IF ((FABS(CheckX - SaveX) > 25.0) OR (FABS(CheckY - SaveY) > 25.0))
				  	CheckX = SaveX
					CheckY = SaveY
					NOUPDATE = 1
				ENDIF
			ENDIF
		ENDIF
	ELSE
		NOUPDATE = 1
		//STAT>KillOff
	ENDIF
	MoveY(0.4)
	IF (!NOUPDATE)
		PointTo(CheckX, CheckY, CheckZ)
		SaveX = CheckX
		SaveY = CheckY
	ENDIF
	frame = 1.0
ENDSTATE


STATE KillOff
	MyFlag = MyFlag | NODISP  
	MyFlag = MyFlag & LNOT(STRATCOLL)
	DeleteBand (trail)
	CREATE SPAWN_BLASTEXP 0.0, 3.0, -2.0, 0.0, 0.0, 0.0, 0
	IF (!parentDead)
		SubParentSem(0, 1)
	ENDIF
	Delete()
ENDSTATE


STATE KillOffFloor
	DeleteBand (trail)
	CREATE SPAWN_BLASTEXP 0.0, 3.0, -2.0, 0.0, 0.0, 0.0, 0
	IF (!parentDead)
		SubParentSem(0, 1)
	ENDIF
	Delete()
ENDSTATE