LOCALFLOAT	time
LOCALFLOAT	px
LOCALFLOAT	py
LOCALFLOAT	pz
LOCALFLOAT	ax
LOCALFLOAT	ay
LOCALFLOAT	az
LOCALFLOAT	tempf
LOCALINT	padPush
LOCALINT	padPress
LOCALINT	noTargets 
LOCALINT	tempi
LOCALINT	pos

DEFINE		ALL_ROUND_SHOCKWAVE		4

STATE Init
  	MyFlag = MyFlag | FRIENDLY | FLOORCOLL
	MyFlag2 = MyFlag2 | PLAYER_CF
	OBJECT>REDDOG\WSMART DOCKLANDS
	SetObjectShadowState(1)
	SetFriction(0.5, 0.5, 0.5)
	time = 0.0
	TRIGSET>Always EVERY 1
	x = DogX
	y = DogY
	SetSecondaryWeaponAmount(0, 1)
	z = DogZ + 0.5
	IF (LeftGunPickUp = 1)
		IF (RightGunPickUp = 1)
			pos = 3
		ELSE
			pos = 2
		ENDIF
	ELSE
		pos = 1
	ENDIF
	AllocStratSoundBlock(1)
	STAT>Start
ENDSTATE

TRIGGER Always
	FacePlayerAim(ParentPlayerNumber)
	time = time + 1.0
	tempf = time * 0.05
	IF (pos = 1)
		px = parentLeftGunX + Ssin(tempf * 2.0) * 0.2
		py = parentLeftGunY + Scos(tempf) * 0.2
		pz = parentLeftGunZ + 1.5 + Ssin(tempf * 2.3) * 0.1
	ENDIF
	IF (pos = 2)
		px = parentRightGunX + Ssin(tempf * 2.0) * 0.2
		py = parentRightGunY + Scos(tempf) * 0.2
		pz = parentRightGunZ + 1.5 + Ssin(tempf * 2.3) * 0.1
	ENDIF
	IF (pos = 3)
		px = parentTopGunX + Ssin(tempf * 2.0) * 0.2
		py = parentTopGunY + Scos(tempf) * 0.2
		pz = parentTopGunZ + 1.5 + Ssin(tempf * 2.3) * 0.1
	ENDIF
	ax = (px - x) * 15.0 
	ay = (py - y) * 15.0
	az = (pz - z) * 15.0
	IF (ScoobyDoo)
		HoldPlayerVY = 4.0
	ENDIF
	x = x + HoldPlayerVX * 0.5
	y = y + HoldPlayerVY * 0.5
	z = z + HoldPlayerVZ * 0.5
	ApplyAbsForce(ax, ay, az, 0, 0, 0, 1.0)
	
	IF (MyParentFlag & NODISP)
		MyFlag = MyFlag | NODISP
	ELSE
		MyFlag = MyFlag & LNOT(NODISP)
	ENDIF
	TRIGFIN
ENDTRIGGER

STATE Start
	IF (MyParentHealth <= 0.0)
		SetSecondaryWeaponType(0, 0)
		Delete()
	ELSE
	
	padPush = GetPadPush(0)
	
	IF (padPush & PADB)
		MODELANIM>REDDOG\WSMART.RDO>EXPLODE>0
		PLAYSOUND>CONCUSS 0 1.0 0.0 0.0 0.0 0
		LOOP (20)
			MyFlag = MyFlag
		ENDLOOP
		SmartBombExplode(0, 1000.0, 501.0)
		CREATE SPAWN_BLASTEXP 0, 0, 0, 0, 0, 0, 0
		//SetSecondaryWeaponType(0, 0)
		//SetSecondaryWeaponAmount(0, 0)
		//Delete()
	ENDIF

	tempi = GetSecondaryWeaponType(0)
	IF (tempi != ALL_ROUND_SHOCKWAVE)
		MyFlag2 = MyFlag2 | TRANSLUCENT
		trans = 1.0
		LOOP (30)
			trans = trans - 0.03333
		ENDLOOP
		Delete()
	ELSE
	   
		IF (PlayerDistXY() > 20.0)
			STAT>Restart
		ENDIF
	ENDIF
	ENDIF
ENDSTATE

STATE Restart
	MyFlag = MyFlag & LNOT(FLOORCOLL)
	LOOP (30)
		MyFlag = MyFlag
	ENDLOOP
	x = DogX
	y = DogY
	z = DogZ + 0.5
	LOOP(2)
		MyFlag = MyFlag
	ENDLOOP
	MyFlag = MyFlag | FLOORCOLL
	STAT>Start
ENDSTATE
